LET nic = \f. \x. x IN
LET ena = \f. \x. f x IN
LET plus = \m. \n. \f. \x. m f (n f x) IN
LET dva = plus ena ena IN
LET naslednik = \n. \f. \x. f (n f x) IN
LET normaliziraj = \n. n naslednik nic IN
LET stiri = plus dva dva IN
LET sest = plus dva stiri IN
LET sedem = naslednik sest IN
LET krat = \m. \n. \f. \x. m (n f) x IN
LET par = \a. \b. \g. g a b IN
LET prvi = \p. p (\x. \y. x) IN
LET drugi = \p. p (\x. \y. y) IN
LET korak = \f. \p. par (drugi p) (f (drugi p)) IN
LET predhodnik = \n. prvi (n (korak naslednik) (par nic nic)) IN
LET true = \x. \y. x IN
LET false = \x. \y. y IN
LET ifthenelse = \b. \x. \y. b x y IN
LET jenic = \n. n (\x. false) true IN
LET psiodfakultete = \g. \n. ifthenelse (jenic n) ena (\f. \x. (krat n (g (predhodnik n))) f x) IN
LET definitivnonefakulteta = \n. sedem IN
LET ipsilon = \psi. (\f. \x. (psi (f f)) x) (\f. \x. (psi (f f)) x) IN
LET fakulteta = \n. (ipsilon psiodfakultete) n IN
normaliziraj (fakulteta (naslednik stiri))